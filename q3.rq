#+ summary: Select the details of a specific heritage object
#+ description: Select the details of a specific heritage object
#+ endpoint: https://api.data.netwerkdigitaalerfgoed.nl/datasets/heritageflix/v1/services/v1/sparql
#+ tags:
#+   - Heritage Objects
#+ defaults:
#+   - heritageObject: http://hdl.handle.net/10934/RM0001.COLLECT.10005

PREFIX schema: <https://schema.org/>

SELECT *
WHERE {
  BIND(?_heritageObject_iri AS ?test)

  ?test a schema:VisualArtwork ;
    schema:name ?name ;
    schema:description ?description ;
    schema:dateCreated ?dateCreated ;
  OPTIONAL { ?test schema:mainEntityOfPage ?webpage }

  # Art period
  ?test schema:temporalCoverage/schema:name ?artPeriodName ;
    schema:temporalCoverage/schema:startDate ?artPeriodStartDate ;
    schema:temporalCoverage/schema:endDate ?artPeriodEndDate .

  # Image
  ?test schema:image/schema:contentUrl ?imageContentUrl ;
    schema:image/schema:encodingFormat ?imageEncodingFormat ;
    schema:image/schema:license ?imageLicense . # TBD: also add name of the license?

  # Creator
  ?test schema:creator/schema:name ?creatorName .

  # Publisher
  ?test schema:publisher/schema:name ?publisherName .
  OPTIONAL { ?test schema:publisher/schema:mainEntityOfPage ?publisherHomepage }
}
